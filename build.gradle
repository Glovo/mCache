buildscript {
    ext.kotlin_version = '1.1.2-4'
    ext.artifactory_user = System.getenv('BIN_USER')
    ext.artifactory_password = System.getenv('API_KEY')

    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.2'
        classpath 'me.tatarka:gradle-retrolambda:3.6.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

ext {
    vName = '1.0.0-SNAPSHOT'
    vCode = 1000
    vSDK = 25
    vMinSDK = 9
    vBuildTools = "25.0.3"
}

allprojects {
    tasks.withType(Javadoc) {
        enabled = false
    }
    repositories {
        jcenter()
    }
}

task bintrayMCache(type: GradleBuild) {
    description = 'mCache publish routine to Bintray'
    group = 'Publishing'
    buildFile = file('mcache/build.gradle')
    startParameter.projectProperties = gradle.startParameter.projectProperties
    tasks = ['install', 'bintrayUpload']
}

task bintrayMCacheKotlin(type: GradleBuild) {
    description = 'mCache Kotlin Extensions publish routine to Bintray'
    group = 'Publishing'
    buildFile = file('mcache-kotlin-extensions/build.gradle')
    startParameter.projectProperties = gradle.startParameter.projectProperties
    tasks = ['install', 'bintrayUpload']
}

task bintrayUploadProject(type: GradleBuild) {
    description = 'mCache publishing to Bintray'
    group = 'Publishing'
    dependsOn bintrayMCache
    dependsOn bintrayMCacheKotlin
}